buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
    }
}

group 'com.ifnoelse'
version '1.0.3'

apply plugin: 'java'
apply plugin: 'signing'
apply plugin: 'maven-publish'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

mainClassName = "com.ifnoelse.pdf.gui.Main"

[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

sourceCompatibility = 1.8


repositories {
    maven {
        url "http://maven.aliyun.com/nexus/content/groups/public/"
    }
}
task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'pdf-bookmark'
                description = 'pdf-bookmark'
                url = 'https://github.com/ifnoelse/pdf-bookmark'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'ifnoelse'
                        name = 'ifnoelse'
                        email = 'ifnoelse@qq.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/ifnoelse/pdf-bookmark.git'
                    developerConnection = 'scm:git:https://github.com/ifnoelse/pdf-bookmark.git'
                    url = 'https://github.com/ifnoelse/pdf-bookmark'
                }
            }
        }
    }
    repositories {
        maven {
            url = './maven'
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile group: 'org.jsoup', name: 'jsoup', version: '1.10.2'
    compile group: 'com.itextpdf', name: 'itextpdf', version: '5.5.10'
    compile group: 'org.jodd', name: 'jodd-http', version: '3.8.1'
    compile group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.58'
}
